# 関数プログラミング入門 Haskellで学ぶ原理と技法 第6章
[全体目次へ戻る](../index.md)

## 6章 木
- 非線形の構造を表す再帰的データ型。分岐構造や情報を乗せる位置などによって様々な種類に分類される

### 二分木
- 分岐において2方向に分岐する木

```haskell
-- Leaf: 1つのデータを持つ葉
-- Fork: 2つの部分木を持つ分岐
data Btree a = Leaf a | Fork (Btree a) (Btree) a
```

- 木の帰納法
  + `_|_`の場合: `P(_|_)`が成り立つ
  + `Leaf x`の場合: `P(Leaf x)`が成り立つ
  + `Fork(xt yt)`の場合: `P(xt)`と`P(yt)`が成り立つことを仮定すれば`P(Fork xt yt))`も成り立つ

- 木の基本的な操作

```haskell
-- 木に含まれる葉の数
size :: Btree a -> Int
size (Leaf x) = 1
size (Fork xt yt) = size xt + size yt

-- 木をリストに変換
flatten :: Btree a -> [a]
flatten (Leaf x) = [x]
flatten (Fork xt yt) = flatten xt ++ flatten yt

-- 内部節点(葉でない節点)の数
-- 定理: size xt = 1 + nodes xt
nodes :: Btree a -> Int
nodes (Leaf x) = 0
nodes (Fork xt yt) = 1 + nodes xt + nodes yt

-- 木の高さ (深さ(根から葉までの距離)の最大値)
height :: Btree a -> Int
height = maxBtree . depths

-- 部分木に含まれる最大要素を返す
maxBtree :: (Ord a) => Btree a -> a
maxBtree (Leaf x) = x
maxBtree (Fork xt yt) = (maxBtree xt) `max` (maxBtree yt)

-- 木の全ての葉の値を、その葉の深さに置き換える
depths :: Btree a -> Btree Int
depths = down 0

-- depthsの内部処理
down :: Int -> Btree a -> Btree Int
down n (Leaf x) = Leaf n
down n (Fork xt yt) = Fork (down (n + 1) xt) (down (n + 1) yt)
```

- 全ての葉の深さが等しい木を「完全な木」という
- 二分木の性質: `height xt < size xt <= 2 ^ height xt`


- 写像と畳み込み

```haskell
-- 木の各要素に関数fを適用
-- リストのmapと同様の法則を満たす
mapBtree :: (a -> b) -> Btree a -> Btree b
mapBtree f (Leaf x) = Leaf (f x)
mapBtree f (Fork xt yt) = Fork (mapBtree f xt) (mapBtree f yt)

-- 木の畳み込み 二分木は以下の構造を持つので、それぞれを置き換える関数を提供する必要がある
-- Leaf :: a -> Btree a
-- Fork :: Btree a -> Btree a -> Btree a
foldBtree :: (a -> b) -> (b -> b -> b) -> Btree a -> b
foldBtree f g (Leaf x) = f x
foldBtree f g (Fork xt yt) = g (foldBtree f g xt) (foldBtree f g yt)

-- 二分木上の多くの演算はfoldBtreeを使って定義できる
-- ただし、constは定数関数(2つ引数を取るが、常に第1引数を返す)
-- const :: a -> b -> a
-- const x y = x
size = foldBtree (const 1) (+)
height = foldBtree (const 0) (<+>)
         where m <+> n = 1 + (m `max` n)
flatten = foldBtree wrap (++)
         where wrap x = [x]
maxBtree = foldBtree id (`max`)
mapBtree f = foldBtree (Leaf . f) Fork
```

- 拡張二分木: 内部節点にも情報を載せることができる二分木
```haskell
-- Forkに付加情報を持つ二分木
-- Fork n xt ytのnについて、n = size xtという条件がついている
data Atree a = Leaf a | Fork Int (Atree a) (Atree a)

-- AtreeのForkの条件を満たすようにForkを生成するスマートコンストラクタ
fork :: Atree a -> Atree a -> Atree a
fork xt yt = Fork (lsize xt) xt yt

lsize :: Atree a -> Int
lsize (Leaf x) = 1
lsize (Fork n xt yt) = n + lsize yt

-- リストからAtreeを生成
mkAtree :: [a] -> Atree a
mkAtree xs
  | (m == 0)  = Leaf (unwrap xs)
  | otherwise = fork (mkAtree ys) (mkAtree zs)
    where m = (length zs) `div` 2
          (ys, zs) = splitAt m xs

-- retrieve xt k = (flatten xt) !! k
-- 付加した情報(左の部分木のsize)を利用することで効率よく実装できる
retrieve :: Atree a -> Int -> a
retrieve (Leaf x) 0 = x
retrieve (Fork m xt yt) k =
  if k < m then retrieve xt k else retrieve yt (k - m)

-- リストをAtreeにしてからretrieveすればO(log n)で済む!
```

### 二分探索木
- 「`flatten`が昇順のリストとなる」という条件つきのラベル付き二分木
  + ラベル付き二分木: 葉に値を持つ代わりに、節点に部分木と「ラベル」と呼ばれる値を持つような構造の二分木

```haskell
data (Ord a) => STree a = Null | Fork (Stree a) a (Stree a)

flatten :: (Ord a) => Stree a -> [a]
flatten Null = []
flatten (Fork xt x yt) = flatten xt ++ [x] ++ flatten yt

-- 上記の条件を満たすことを利用して、効率的な検索を行う
member :: (Ord a) => a -> Stree a -> Bool
member x Null = False
member x (Fork xt y yt)
  | (x <  y) = member x xt
  | (x == y) = True
  | (x >  y) = member x yt

-- リストを二分探索木に変換する
-- partitionは条件を取って、その条件を満たすものとそうでないものに分割する関数
mkStree :: (Ord a) => [a] => Stree a
mkStree [] = Null
mkStree (x : xs) = Fork (mkStree ys) x (mkStree zs)
                   where (ys, zs) = partition (<= x) xs

-- flattenすれば昇順になることがわかっているので以下の実装は正しい
sort :: (Ord a) => [a] -> [a]
sort = flatten . mkStree                   

-- 二分探索木に要素を追加
insert :: (Ord a) => a -> Stree a -> Stree a
insert x Null = Fork Null x Null
insert x (Fork xt y yt)
  | (x <  y) = Fork (insert x xt) y yt
  | (x == y) = Fork xt y yt
  | (x >  y) = Fork xt y (insert x yt)

-- 二分探索木から要素を削除し、その結果を返す
delete :: (Ord a) => a -> Stree a -> Stree a
delete x Null = Null
delete x (Fork xt y yt)
  | (x <  y) = Fork (delete x xt) y yt
  | (x == y) = join xt yt
  | (x >  y) = Fork xt y (delete x yt)

-- 以下の条件を満たす関数
-- flatten (join xt yt) == flatten xt ++ flatten yt
-- 右部分木の一番左にある要素を持ち上げることで、高さが高くならないようにできる

-- 以下の仕様を満たす2つの関数があればjoinを実装可能
-- headTree :: (Ord a) => Stree a -> a
-- headTree = head . flatten

-- tailTree :: (Ord a) => Stree a -> Stree a
-- flatten . tailTree = tail . flatten

join :: (Ord a) => Stree a -> Stree a -> Stree a
join xt yt = if empty yt then xt else Fork xt w wt
             where (w, wt) = splitTree yt

empty :: (Ord a) => Stree a -> Bool
empty Null = True
empty (Fork xt x yt) = False

-- (headTree xt, tailTree xt)を返す関数
splitTree :: (Ord a) => Stree a -> (a, Stree a)
splitTree (Fork xt y yt) =
  if empty xt then (y, yt) else (x, Fork wt y yt)
  where (x, wt) = splitTree xt
```

### 二分ヒープ木
- 「各節点のラベルは、どちらの部分木のラベルよりも大きくない」という条件つきのラベル付き二分木

```haskell
data (Ord a) => Htree a = Null | Fork a (Htree a) (Htree a)

-- flattenすると要素が昇順に並ぶように定義する
flatten :: (Ord a) => Htree a -> [a]
flatten Null = []
flatten (Fork x xt yt) = x : merge (flatten xt) (flatten yt)

merge :: (Ord a) => [a] -> [a] -> [a]
merge [] ys = ys
merge (x : xs) [] = x : xs
merge (x : xs) (y : ys) =
  if x <= y
    then x : merge xs (y : ys)
    else y : merge (x : xs) ys

-- リストからヒープ木を構成する
-- mkHtreeはリストから木の「構造」だけを作る(条件を満たしているとは限らない)
mkHeap :: (Ord a) => [a] -> HTree a
mkHeap = heapify . mkHtree

-- Htreeがヒープ木の条件を満たすようにする
heapify :: (Ord a) => Htree a -> Htree a
heapify Null = Null
heapify (Fork x xt yt) = sift x (heapify xt) (heapify yt)

-- ヒープ木の条件を満たすように、与えられた値を適切な場所に入れる
sift :: (Ord a) => a -> Htree a -> Htree a -> Htree a
sift x Null Null = Fork x Null Null
sift x (Fork y a b) Null =
  if x <= y then Fork x (Fork y a b) Null else Fork y (sift x a b) Null
sift x Null (Fork z c d) =
  if x <= z then Fork x Null (Fork z c d) else Fork y Null (Fork x c d)
sift x (Fork y a b) (Fork z c d)
  | x <= (y `min` z) = Fork x (Fork y a b) (Fork z c d)
  | y <= (x `min` z) = Fork y (sift x a b) (Fork z c d)
  | z <= (x `min` y) = Fork z (Fork y a b) (sift x c d)

-- ヒープソート
sort :: (Ord a) => [a] -> [a]
sort = flatten . mkHeap
```


### 多分木
- 多分岐構造を持つ木

```haskell
-- 多分木 = ラベルと部分木のリスト
-- 外部節点は部分木を持たない(部分木のリストが空)
data Rose a = Node a [Rose a]
```

- 有限分岐木(すべての節点の部分木の数が有限な多分木)に対する帰納法
  + `_|_`の場合: `P(_|_)`が成り立つ
  + `Node x xts`の場合: `xts`のすべての要素`xt`について`P(xt)`が成り立つことを仮定すれば`P(Node x xts)`が成り立つ

- 有限多分木は一意に有限二分木で表現できる。その逆も同様
  + カリー化されていない式とカリー化されている式を対応付けるのと同じ

- すべてのラベルを列挙する関数`flatten`は以下のように定義できる
  + この`flatten`は深さ優先順にラベルを列挙する
  + ちなみに、幅優先順の列挙は非常に大きな空間を必要とする

```haskell
flatten :: Rose a -> [a]
flatten (Node x xts) = x : concat (map flatten xts)
```


### ハフマン木
- ハフマン符号: 情報圧縮アルゴリズムの1つ。符号化されたテキストの長さの期待値が最小となるようになっている
  + よく出現する文字の符号ほど短くなる
  + 文字に対応する符号を「ハフマン木」という二分木で表すことができる

- ハフマン符号化の手順
  1. サンプルから情報を収集する
  2. 二分木を構成する
  3. 符号化する
  4. 復号する

#### 符号化と復号
- 復号: ビット列の語頭を辿って葉に達するまで木を辿り、その葉が持つ文字に変換する

```haskell
decede :: Btree Char -> [Bit] -> [Char]
decode t cs =
  if null cs then [] else decode1 t cs
  where decode1 (Leaf x) cs
        decode1 (Fork xt yt) (0 : cs) = decode1 xt cs
        decode1 (Fork xt yt) (1 : cs) = decode1 yt cs
```

- 符号化: 符号化を効率良く行うに、まずハフマン木を「文字とその符号の対応表」に変換する

```haskell
-- 効率化の余地あり!
-- hufmergeで毎回ビット列の長さを再計算しないようにするために、CodeTableのエントリにビット列の長さも追加すれば…
type CodeTable = [(Char, [Bit])]
transform :: Btree Char -> CodeTable
transform (Leaf x) = [(x, [])]
transform (Fork xt yt) = hufmerge (transform xt) (transform yt)

hufmerge :: CodeTable -> CodeTable -> CodeTable
hufmerge [] ycs = [(y, 1 : cs) | (y, cs) <- ycs]
hufmerge xbs [] = [(x, 0 : bs) | (x, bs) <- xbs]
hufmerge ((x, bs) : xbs) ((y, cs) : ycs)
  | length bs <= length cs = (x, 0 : bs) : hufmerge xbs ((y, cs) : ycs)
  | otherwise              = (y, 1 : cs) : hufmerge ((x, bs) : xbs) ycs

encode :: Btree Char -> [Char] -> [Bit]
encode t = concat . map (lookup codeTable)
           where codeTable = transform t

lookup :: CodeTable -> Char -> [Bit]
lookup ((x, bs) : xbs) y = if x == y then bs else lookup xbs y        
```

#### サンプルの分析
- サンプルにおける文字の相対頻度を計算するには、文字列を整列し、同じ文字の並びをその文字と文字数の対に変換すればいい

```haskell
-- a型から何らかの情報(b型)を抜き出し、その情報に基づいてソート
sortBy :: (Ord b) => (a -> b) -> [a] -> [b]

sample :: [Char] -> [(Char, Int)]
sample = sortBy freq . collate . sortby id

collate :: [Char] -> [(Char, Int)]
collate [] = []
collate (x : xs) = (x, 1 + length ys) : collate zs
                   where (ys, zs) = span (== x) xs

freq :: (Char, Int) => Int
freq (x, m) = m                  
```

#### ハフマン木の組み立て
- 文字と出現頻度(=重み)の対を葉(Tip)のリストにし、それをまとめていくことで構成する
  + 分岐部の重みは、それがもつ2つの部分木の重みの和とする

```haskell
data Huff = Tip Int Char | Node Int Huff Huff

-- Huffから重みの情報を取り去る
unlabel :: Huff -> Btree Char
unlabel (Tip w x) = Leaf x
unlabel (Node w xt yt) = Fork (unlabel xt) (unlabel yt)

-- 文字と出現頻度の対のリストをハフマン木に変換
-- 全ての対を1要素の木に変換してから、全体が1つの木になるまでcombineによる部分木の併合を繰り返す
mkHuff :: [(Char, Int)] -> Huff
mkHuff = unwrap . until singleton combine . map mktip

mktip :: (Char, Int) -> Huff
mktip (c, w) = Tip w c

-- 2つの部分木をまとめる
combine :: [Huff] -> [Huff]
combine (xt : yt : xts) = insert (Node w xt yt) xts
                          where w = weight xt + weight yt

weight :: Huff -> Int
weight (Tip w c) = w
weight (Node w x y) = w

-- 重みの昇順という条件を保ちながら、Huffをリストに追加
insert :: Huff -> [Huff] -> [Huff]
insert xt yts = uts ++ [xt] ++ vts
                where (uts, vts) = span p yts
                      p yt = (weight yt <= weight xt)
```

***

[前へ](c5.md) /
[全体目次へ戻る](../index.md) /
[次へ](c7.md)
