# 関数プログラミング入門 Haskellで学ぶ原理と技法 第4章
[全体目次へ戻る](../index.md)

## 4章 リスト
- 2つのリストが等しいのは、同じ値が同じ順序で含まれているときかつそのときに限る

### データ型としてのリスト
- 空リストからはじめて、要素を1つずつ追加していくという考え方で構成する

```haskell
-- Cons はリストの先頭に要素を1つ追加する
data List a = Nil | Cons a (List a)

-- Nil = [], Cons = (:) ただし(:)は右結合なので、括弧は省略できる
[1, 2, 3] = Cons 1 (Cons 2 (Cons 3 Nil)) = 1 : 2 : 3 : []
```

- リスト上の関数はパターンマッチを使って定義できる

```haskell
instance (Eq a) => Eq [a] where
  [] == [] = True
  [] == (y : ys) = False
  (x : xs) == [] = False
  (x : xs) == (y : ys) = (x == y) && (xs == ys)

-- リストが空リストに等しいかどうかを判定する場合は、中身の型がEqのインスタンスである必要はない
null :: [a] -> Bool
null [] = True
null (x : xs) = False
```

### リスト上の帰納法
- ある命題`P`がすべてのリストで成り立つことを証明するには、以下の3つを証明すれば良い
  + `_|_`の場合: `P(_|_)`が成り立つ
  + `[]`の場合: `P([])`が成り立つ
  + `x : xs`の場合: `P(xs)`が成り立つならば、全ての`x`について`P(x : xs)`が成り立つ


### リスト演算
- 連接`(++)`: 2つのリストをつなげたもの
  + `xs ++ _|_`は「擬リスト」である。途中の要素までは計算できるが、`_|_`以降の計算は停止しない(またはエラー)
  + `xs`が擬リストまたは無限リストならば、`xs ++ ys == xs`が成り立つ
  + 連接は`[]`を単位元とする結合的演算である

```haskell
(++) :: [a] -> [a] -> [a]
[] ++ ys       = ys
(x : xs) ++ ys = x : (xs ++ ys)

(xs ++ ys) ++ zs == xs ++ (ys ++ zs)
xs ++ [] == [] ++ xs == xs
```

- `concat`: 「リストのリスト」の内側の各リストを順につなげて、1つのリストにする
- `reverse`: 有限リストの要素順を逆にする
- `length`: 有限リストの長さ
- `head`, `tail`, `init`, `last`: リストの先頭・先頭以外・末尾以外・末尾
  + `head`, `tail`は定数時間で計算できるが、`init`, `last`はできない
- `take`, `drop`: 非負整数`n`とリストをとって、最初の`n`要素を取ったリスト/除いたリストを返す
  + これらの結果をまとめて返す`splitAt`という関数もある
- `!!`: 整数`n`をとって、リストの`n`番目の要素を取得する


### `map`と`filter`
- `map`: 関数をリストの各要素に適用する
- `[m .. n]`: `m`から`n`までの整数からなるリスト `[m ..]`: `m`以上の整数からなる無限リスト
- `map`に関して以下の恒等式が成り立つ

```haskell
-- 全ての要素に恒等関数を適用しても、リストに変化はない
map id == id
-- 全ての要素にgを適用してからfを適用するのと、全ての要素にfとgを合成したものを適用するのは同じこと
-- 式を右辺から左辺の形に変形すれば、リストの走査回数を減らすことができる
map (f . g) == map f . map g

-- 自然性条件: 「操作してから関数を適用しても、関数を適用してから操作しても結果は同じ」
    f . head    == head    . map f
map f . tail    == tail    . map f
map f . reverse == reverse . map f
map f . concat  == concat  . map (map f)
```

- `filter`: ある条件を表す関数をとって、それを満たす要素だけを残したリストを返す
- `filter`に関して以下の等式が成り立つ

```haskell
-- (p `and` q) x = p x && q x
-- qでfilterしてさらにpでfilterするのは、pかつqでfilterするのと同じこと
filter p . filter q == filter (p `and` q)

-- 繋いでからfilterしてもfilterしてから繋いでも結果は同じ
filter p . concat == concat . map (filter p)
```

### `zip`
- `zip`: 2つのリストをとり、対応する要素の組のリストを返す

```haskell
zip :: [a] -> [b] -> [(a, b)]
zip [] ys = []
zip (x : xs) [] = []
zip (x : xs) (y : ys) = (x, y) : zip xs ys

-- 使用例
-- 2つの数値のリストをベクトルとみなして内積を計算する
-- 対応する要素同士を掛けた結果の総和を取ればいい
sp :: (Num a) => [a] -> [a] -> a
sp xs ys = sum (map times (zip xs ys))
           where times (x, y) = x * y

-- 与えられたリストが非減少列かどうか判定する
-- zip xs (tail xs)はリストの隣り合った要素同士を組にしたリストとなる
-- and :: [Bool] -> Bool は、リストの全ての要素がTrueのときTrueを返す
nondec :: (Ord a) => [a] -> Bool
nondec xs = and (map leq (zip xs (tail xs)))
            where leq (x, y) = (x <= y)

-- 指定された要素が出現する場所のリスト
positions :: (Eq a) => a -> [a] -> [Int]
positions x xs = [i | (i, y) <- zip [0..] xs, x == y]

-- 指定された要素がリストの何番目に出現するかを返す(なければ-1)
position :: (Eq a) => a -> [a] -> Int
position x xs = head (positions x xs ++ [-1])
```

- `zipWith`: 2つのリストの対応する要素に対し、2引数関数を適用した結果を集めたリストを返す
  + `zip`して`map`するというパターンを1つの関数で実現可能

```haskell
zipWith :: (a -> b -> c) -> ([a] -> [b] -> [c])
zipWith f xs ys = map (uncurry f) (zip xs ys)

-- 「zipしてmap」をzipWithで置き換えると簡潔な定義になる
sp xs ys = sum (zipWith (*) xs ys)
nondec xs = and (zipWith (<=) xs (tail xs))
```

- `unzip`: 組のリストをとり、それを分けて2つのリストにする

```haskell
unzip :: [(a, b)] -> ([a], [b])
unzip = pair (map fst, map snd)
```


### 畳み込み関数
- 自然数`Nat`と同様に、リストに対しても畳み込みを定義できる

```haskell
-- 二項演算<+>、ある定数e、そしてリストをとって、
-- リストが[]ならeに、x : xsならx <+> xsに置き換えた式を計算する
-- foldr <+> e (x1 : x2 : x3 : []) == x1 <+> (x2 <+> (x3 <+> e))
foldr :: (a -> b -> b) -> b -> [a] -> b
foldr f e [] = e
foldr f e (x : xs) = f x (foldr f e xs)

-- foldrの双対となる演算
-- 式のグループ化の方向と初期値の位置が右ではなく左になっている
-- foldl <+> e (x1 : x2 : x3 : []) == ((e <+> x1) <+> x2) <+> x3
foldl :: (b -> a -> b) -> b -> [a] -> b
foldl f e [] = e
foldl f e (x : xs) = foldl f (f e x) xs
```

- 二項演算`<+>`が結合的で単位元`e`を持つならば、`foldr`と`foldl`の結果は等しい

#### 空でないリスト上の畳込み
- リストが空でないときのみ結果が定義される畳み込みは以下のように定義できる

```haskell
foldr1 :: (a -> a -> a) -> [a] -> a
foldr1 f (x : xs) = if null xs then x else f x (foldr1 f xs)

foldl1 :: (a -> a -> a) -> [a] -> a
foldl1 f (x : xs) = foldl f x xs
```

#### 走査関数
- `foldl`や`foldr`の「途中結果を保存する版」を走査(`scanl`, `scanr`)という

```haskell
-- inits [x, y, z] = [[], [x], [x, y], [x, y, z]]
inits :: [a] -> [[a]]
inits [] = [[]]
inits (x : xs) = [] : map (x :) (inits xs)

-- 愚直な実装(効率が悪い)
scanl :: (b -> a -> b) -> b -> [a] -> [b]
scanl f e = map (foldl f e) . inits

-- 効率の良い実装
scanl' :: (b -> a -> b) -> b -> [a] -> [b]
scanl' f e [] = [e]
scaml' f e (x : xs) = e : scanl f (f e x) xs

-- scanrはscanlの双対
-- tails [x, y, z] = [[x, y, z], [y, z], [z], []]
tails :: [a] -> [[a]]
tails [] = [[]]
tails (x : xs) = (x : xs) : tails xs

-- 愚直な実装
scanr :: (a -> b -> b) -> b -> [a] -> [b]
scanr f e = map (foldr f e) . tails

-- 効率の良い実装
scanr' :: (a -> b -> b) -> b -> [a] -> [b]
scanr' f e [] = [e]
scanr' f e (x : xs) = f x (head ys) : ys
                      where ys = scanr f e xsg
```

### 畳み込み則
- 双対定理
  1. `<+>`を単位元`e`をもつ結合的演算とすると、任意の有限リストに対して`foldr <+> e`と`foldl <+> e`の結果は等しい
  2. `<+>`と`<*>`が互いに結合的で`x <+> e == e <*> x`ならば、任意の有限リストに対して`foldr <+> e`と`foldl <*> e`の結果は等しい
  3. 任意の有限リストについて`foldr f e xs == foldl (flip f) e (reverse xs)`が成り立つ

- 融合定理
 + `f`が正格で`f a = b`かつ`f (g x y) = h x (f y)`であれば`f . foldr g a == foldr h b`である
 + `f`が正格で`f a = b`かつ`f (g x y) = h (f x) y`であれば`f . foldl g a == foldl h b`である

- `fold-map`融合則
  + 「`map`して畳み込む」という操作は1つの畳み込みにまとめることができる
  + `foldr f a . map g == foldr (f . g) a`

- 「簿記の法則」
  + 「`concat`してから畳み込む」のと「内側を畳み込んでから、さらにその結果を同じ関数で畳み込む」のは同じこと。ただし関数が結合的で`a`が単位元であるときに限る
  + `foldr f a . concat == foldr f a . map (foldr f a)`
  
***

[前へ](c3.md) /
[全体目次へ戻る](../index.md) /
[次へ](c5.md)
