# 関数プログラミング入門 Haskellで学ぶ原理と技法 第11章
[全体目次へ戻る](../index.md)

## 11章 構文解析
### 連接
- 構文解析器を「文字列を入力としてとり、『解析結果と未消費の文字列の対』のリストを出力として返す関数」と考える
  + リストの各要素は、ありうる全ての場合を表す

```haskell
newtype Parser a = MkP (String -> [(a, String)])

apply :: Parser a -> String -> [(a, String)]
apply (MkP f) s = f s

-- 解析結果を1つだけ返す関数
applyParser :: Parser a -> String -> a
applyParser p = fst . head . apply p

-- Parserをモナドのインスタンスにできる
instance Monad Parser where
  return x = MkP f where f s = [(x, s)]
  p >>= q  = MkP f
    where f s = [(y, s'') | (x, s') <- apply p s, (y, s'') <- apply (q x) s']
```

- Parserをモナドとしたので`do`記法を使って構文解析器を複合できる。まずは基本となる構文解析器を定義する

```haskell
-- 最初の1文字を読み込む。入力が全て消費されていれば失敗
item :: Parser Char
item MkP f
  where f []       = []
        f (c : cs) = [(c, cs)]

-- 常に失敗する
zero :: Parser a
zero = MkP f where f s = []

-- 与えられた条件を満たす文字を読み込む
sat :: (Char -> Bool) -> Parser Char
sat p = do
  c <- item
  if p c then return c else zero

-- 指定した文字を受け入れる
char :: Char -> Parser ()
char x = do
  c <- sat (== x)
  return ()

-- 指定した文字列を受け入れる
string :: String => Parser ()
string [] = return ()
string (x : xs) = do
  char x
  string xs
  return ()

-- 小文字を読み込んで結果を返す
lower :: Parser Char
lower = sat isLower

-- 数字を読み込んで、整数に変換して返す
digit :: Parser Int
digit = do
  d <- sat isDigit
  return (ord d - ord '0')
```

### 選択
- 解析結果の選択肢を複合する演算子`plus`を定義する

```haskell
-- 入力sをとり、pとqをそれぞれsに適用した結果を合わせて返す
plus :: Parser a -> Parser a -> Parser a
p `plus` q = MkP f where f s = apply p s ++ apply q s
```

- `plus`は`zero`を単位元とするモノイドを構成する

```haskell
zero `plus` p == p
p `plus` zero == p
p `plus` (q `plus` r) == (p `plus` q) `plus` r
```

- `plus`は`>>=`の上で分配法則を満たす

```haskell
(p `plus` q) >>= r == (p >>= r) `plus` (q >>= r)
```

- `plus`の使用例

```haskell
-- 入力の先頭に小文字の連続部分があれば、全ての場合を返す
-- 複数ある場合は、消費文字数が一番長い結果が先頭に来る
lowers :: Parser String
lowers = lower >>= (\c -> lowers >>= (\cs -> return (c : cs))) `plus` return ""
```

- 通常は、入力の先頭部分を最も長く消費した結果だけに興味がある。そのような結果だけを返す選択演算子を定義できる
  + 1つ目が失敗するとバックトラックして2つ目を試すので、効率が悪い

```haskell
-- 入力sに対し、pが成功したらその結果を返し、pが失敗したらもとの入力をqに適用してその結果を返す(バックトラッキング)
orelse :: Parser a -> Parser a -> Parser a
p `orelse` q = MkP f
               where f s = if null ps then apply q s else ps
                           where ps = apply p s
```

***

[前へ](c10.md) /
[全体目次へ戻る](../index.md) /
[次へ](c12.md)
